class AlarmChannel

instance variables

public type : Utils`Type;
public signalTime : nat;
public status : Utils`Status;

operations

public AlarmChannel: Utils`Type ==> AlarmChannel
	AlarmChannel(alarmType) == (
		type := alarmType;
		signalTime := 0;
		status := Utils`turnOff();
		return self;
		);		

/**
* Turn On Alarm
*/
public turnOn : () ==> ()
	turnOn() == 
		status := Utils`turnOn(type);

/**
* Turn Off Alarm
*/
public turnOff : () ==> ()
	turnOff() == 
		status := Utils`turnOff();

/**
* Activate alarm and register the timestamp
*/
public activateAlarm : nat ==> ()
	activateAlarm(time) == (
			status := Utils`activateAlarm(type);
			signalTime := time
			);

/**
* Deactivate alarm and put timeStamp null
*/
public desactivateAlarm : () ==> ()
	desactivateAlarm() == (
			status := Utils`desactivateAlarm(type);
			signalTime := 0
			)
	pre status in set {<ALARM>, <INTRUSION>};

/**
* Update alarm status after each update time
*/
public updateAlarm : nat ==> ()
	updateAlarm(t) == (
		if t > signalTime + 20 then (
					status := Utils`turnOn(type);
					signalTime := 0
					)
		elseif 	t > signalTime + 5 then status := Utils`ringAlarm() 
	)
	pre status <> <OFF>

end AlarmChannel
